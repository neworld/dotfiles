initscript {
  repositories {
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
  }
}

allprojects {
  apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
          selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
        }
        if (rejected) {
          selection.reject('Release candidate')
        }
      }
    }
  }
}

gradle.settingsEvaluated { settings ->
    settings.plugins.withId("com.gradle.enterprise") {
        settings.gradleEnterprise.buildScan { 
            termsOfServiceUrl = "https://gradle.com/terms-of-service"; termsOfServiceAgree = "yes" 
        }
    }
}
